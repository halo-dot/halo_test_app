// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.6.21'
    ext.dokka_version = '1.4.32'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }

    }

    dependencies {
        // NOTE: Do not place your application dependencies here;
        // they belong in the individual module build.gradle files

        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'com.github.kezong:fat-aar:1.3.8'
    }
}

ext {
    Properties properties = new Properties()
    def propertiesFile = project.rootProject.file('local.properties')
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
    }

    accessKey = properties.getProperty('aws.accessKey')
    secretKey = properties.getProperty('aws.secretKey')
    tokenKey = System.getenv('AWS_SESSION_TOKEN')
}

allprojects {
    repositories {
        def repos = ['releases', 'snapshots']
        repos.each { repo ->
            maven {
                name = repo
                url = "s3://synthesis-halo-artifacts/$repo"

                credentials(AwsCredentials) {
                    accessKey = rootProject.ext.accessKey
                    secretKey = rootProject.ext.secretKey
                    if (tokenKey != null) {
                        sessionToken = rootProject.ext.tokenKey
                    }
                }
            }
        }

        google()
        mavenCentral()

        maven {
            url 'https://jitpack.io'
        }

        flatDir {
            dirs "${rootProject.projectDir}/sdk/libs"
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}